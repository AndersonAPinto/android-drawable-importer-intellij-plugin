plugins {
    id "org.jetbrains.intellij" version "0.1.10"
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

sourceSets {
    main {
        resources {
            exclude 'assets/android_icons'
            exclude 'assets/material_icons'
        }
    }
}

intellij {
    version project.hasProperty('ideaVersion') ? ideaVersion : 'IC-2016.2.4'
    pluginName 'android-drawable-importer-intellij-plugin'
    plugins 'android'
    updateSinceUntilBuild false
    sameSinceUntilBuild false
    downloadSources !Boolean.valueOf(System.getenv('CI'))

    publish {
        username 'Prengepower'
        pluginId '7658'

        password System.getenv('PLUGIN_PASSWORD')
        channel System.getenv('PLUGIN_CHANNEL') ?: ''
    }
}

group 'de.mprengemann.intellij.plugin.androidicons'

def majorVersion = '0.6'
version "${Boolean.valueOf(System.getenv('CI')) ? "$majorVersion-${System.getenv('TRAVIS_BUILD_NUMBER')}" : "$majorVersion"}"

repositories {
    mavenCentral()
}

dependencies {
    implementation  'commons-io:commons-io:2.4'
    implementation  'org.apache.commons:commons-math3:3.4.1'
    implementation  'org.imgscalr:imgscalr-lib:4.2'
    implementation  'net.coobird:thumbnailator:0.4.8'
    implementation  'com.google.code.gson:gson:2.3.1'
}
